# Maintainer: Ainola

pkgname=gog-risk-of-rain
pkgver=2.0.0.4
pkgrel=1
pkgdesc="Risk of Rain is an action platformer with roguelike elements."
url="http://terraria.org/"
license=('custom')
arch=('i686' 'x86_64')
depends=('sdl2')
source=("gog://${pkgname//-/_}_${pkgver}.sh"
        "${pkgname}.desktop")
sha256sums=('4695e607e815858735faf939b61a2be7a574d35e17c775a248ec5fdc5cdf3438'
            'd23c53abb6c74cde07044ddc084fc2be2ffb0371ebc7d699a2d7da798262286e')

# You need to download the gog.com installer file manually or with lgogdownloader.
DLAGENTS+=("gog::/usr/bin/echo %u - This is is not a real URL, you need to download the GOG file manually to \"$PWD\" or setup a gog:// DLAGENT. Read this PKGBUILD for more information.")

# Prevent compressing final package
PKGEXT='.pkg.tar'

prepare(){
    # Unzip will produce an error code because it is unable to unzip the Installer.
    # Therefore, a conditional into a no-op command will keep the PKGBUILD from failing
    # Of course, if you have any real problems unzipping the PKGBUILD will not abort.
    #unzip -d "${srcdir}/risk-of-rain" "${pkgname//-/_}_${pkgver}.sh" || :
    cd "${srcdir}/data/noarch"

    # We must `cd` to the install dir before launching since the /usr/bin/
    # launcher is merely a link. It expects a certain folder structure.
#    sed -r -i \
#        's/(declare -r CURRENT_DIR="\$\( cd "\$\( dirname )'`
#          `'"\$\{BASH_SOURCE\[0\]\}"(.*$)'`
#          `'/\1$( readlink -nf "${BASH_SOURCE[0]}" )\2/' \
#        "start.sh"
    sed -r -i \
        's/(CURRENT_DIR="\$\( cd "\$\( dirname )'`
          `'"\$\{BASH_SOURCE\[0\]\}"(.*$)'`
          `'/\1$( readlink -nf "${BASH_SOURCE[0]}" )\2/' \
        "start.sh"
}
package(){
    cd "${srcdir}/data/noarch"
    # Install game
    install -d "${pkgdir}/opt/${pkgname}/"
    install -d "${pkgdir}/opt/${pkgname}/support"
    install -d "${pkgdir}/usr/bin/"
    cp -r "game/" "${pkgdir}/opt/${pkgname}/"
    cp -r "lib/" "${pkgdir}/opt/${pkgname}/"
    install -Dm755 "start.sh" \
        "${pkgdir}/opt/${pkgname}/"
    install -Dm755 "gameinfo" \
        "${pkgdir}/opt/${pkgname}/"
    install -Dm755 support/*.{sh,shlib} -t \
        "${pkgdir}/opt/${pkgname}/support"

    # Desktop integration
    install -Dm 644 "support/icon.png" \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm644 "docs/End User License Agreement.txt" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm 644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    ln -s "/opt/${pkgname}/start.sh" "${pkgdir}/usr/bin/${pkgname}"
}
